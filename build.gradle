plugins {
    id 'java'
    id 'java-library'
    id 'idea'

    id "application"
    id 'org.openjfx.javafxplugin' version '0.0.8'

    id 'org.springframework.boot' version '2.3.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
}

ext {
    testNG = 'org.testng:testng:7.1.0'
    jsr305 = 'com.google.code.findbugs:jsr305:3.0.2'
    javaxInject = 'javax.inject:javax.inject:1'
    javaVersion = JavaVersion.VERSION_14
}

wrapper {
    description 'Setup the Gradle wrapper'
    gradleVersion = "6.4.1"
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

allprojects {
    group = 'com.ak'
    version = '2020.06.03'
    apply plugin: 'idea'
    idea {
        module {
            inheritOutputDirs = true
        }
    }

    sourceSets {
        main.output.resourcesDir = main.java.outputDir
    }

    apply plugin: 'java'
    apply plugin: 'java-library'

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url = 'https://plugins.gradle.org/m2/'
        }
        jcenter()
    }

    dependencies {
        implementation jsr305
        testImplementation testNG
    }

    test {
        forkEvery = 5
        useTestNG()
        maxHeapSize = '1G'
    }
}

dependencies {
    implementation javaxInject
    implementation 'org.springframework.boot:spring-boot-starter'
}

configurations {
    all {
        exclude group: 'jakarta.annotation', module: 'jakarta.annotation-api'
    }
}

processResources {
    expand(project.properties)
}

javafx {
    version = '14'
    modules = ['javafx.controls', 'javafx.fxml']
}

mainClassName = "com.ak.fx.desktop.FxApplication"