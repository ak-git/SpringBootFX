plugins {
    id 'java'
    id 'java-library'
    id 'idea'
    id "application"
    id "org.javamodularity.moduleplugin" version '1.8.11'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id "com.github.spotbugs" version '5.0.9'
    id 'org.springframework.boot' version '2.7.1'
    id 'io.spring.dependency-management' version '1.0.12.RELEASE'
}

ext {
    jsr305 = 'com.google.code.findbugs:jsr305:3.0.2'
    javaxInject = 'javax.inject:javax.inject:1'
    jSerialComm = 'com.fazecast:jSerialComm:[2.7.0,3.0.0)'
    javaVersion = JavaVersion.VERSION_18
}

wrapper {
    description 'Setup the Gradle wrapper'
    gradleVersion = "7.5"
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

allprojects {
    group = 'com.ak'
    version = '2022.07.17'
    apply plugin: 'idea'
    idea {
        module {
            inheritOutputDirs = true
        }
    }

    sourceSets {
        main.output.resourcesDir = main.java.classesDirectory
    }

    apply plugin: 'java'
    apply plugin: 'java-library'

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url = 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        implementation jsr305
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
        forkEvery = 5
        maxHeapSize = '1G'
    }
}

dependencies {
    implementation javaxInject
    implementation jSerialComm
    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

configurations {
    all {
        exclude group: 'jakarta.annotation', module: 'jakarta.annotation-api'
        exclude group: 'com.vaadin.external.google', module: 'android-json'
    }
}

processResources {
    expand(project.properties)
}

javafx {
    version = '18.0.1'
    modules = ['javafx.controls', 'javafx.fxml']
}

application {
    mainModule = 'com.ak.fx.desktop'
    mainClass = 'com.ak.fx.desktop.FxApplication'
}