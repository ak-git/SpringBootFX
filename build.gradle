plugins {
    id 'java'
    id 'java-library'
    id 'idea'
    id "application"
    id "org.javamodularity.moduleplugin" version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id "com.github.spotbugs" version '5.0.13'
    id 'org.springframework.boot' version '3.0.4'
    id 'io.spring.dependency-management' version '1.1.0'
}

ext {
    jsr305 = 'com.google.code.findbugs:jsr305:3.0.2'
    javaxInject = 'jakarta.inject:jakarta.inject-api:2.0.1.MR'
    jSerialComm = 'com.fazecast:jSerialComm:[2.7.0,3.0.0)'
    javaVersion = JavaVersion.VERSION_19
}

wrapper {
    description 'Setup the Gradle wrapper'
    gradleVersion = "8.0.2"
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

allprojects {
    group = 'com.ak'
    version = '2023.03.04'
    apply plugin: 'idea'
    idea {
        module {
            inheritOutputDirs = true
        }
    }

    sourceSets {
        main.output.resourcesDir = main.java.classesDirectory
    }

    apply plugin: 'java'
    apply plugin: 'java-library'

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url = 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        implementation jsr305
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
        forkEvery = 5
        maxHeapSize = '1G'
    }
}

dependencies {
    implementation javaxInject
    implementation jSerialComm
    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

configurations {
    configureEach {
        exclude group: 'jakarta.annotation', module: 'jakarta.annotation-api'
        exclude group: 'com.vaadin.external.google', module: 'android-json'
    }
}

processResources {
    expand(project.properties)
}

javafx {
    version = '19'
    modules = ['javafx.controls', 'javafx.fxml']
}

application {
    mainModule = 'com.ak.fx.desktop'
    mainClass = 'com.ak.fx.desktop.FxApplication'
}