import com.github.spotbugs.snom.Confidence

plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    alias(libs.plugins.spotbugs)
    alias(libs.plugins.ben.manes.versions)
    alias(libs.plugins.jacocolog)
    alias(libs.plugins.jgitver)
}

spotbugs {
    reportLevel = Confidence.valueOf('HIGH')
}

tasks.spotbugsMain {
    reports.create('html') {
        required = true
        setStylesheet('fancy-hist.xsl')
    }
}

allprojects {
    group = 'com.ak'
    apply plugin: 'idea'
    idea {
        module {
            inheritOutputDirs = true
        }
    }

    sourceSets {
        main.output.resourcesDir = main.java.classesDirectory
    }

    apply plugin: 'jacoco'
    apply plugin: 'org.barfuin.gradle.jacocolog'

    jacoco {
        toolVersion = '0.8.14'
    }

    jacocoTestReport {
        reports {
            xml.required = true
        }
    }
}

subprojects {
    apply plugin: 'java'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(25)
        }
    }

    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-parameters'
    }

    compileTestJava {
        options.encoding = 'UTF-8'
    }

    test {
        forkEvery = 5
        useJUnitPlatform()
        testLogging {
            events 'passed', 'skipped', 'failed'
        }
        maxHeapSize = '1G'
    }
}