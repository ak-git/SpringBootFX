plugins {
    id 'application'
    alias(libs.plugins.javafxplugin)
    alias(libs.plugins.springframework.boot)
    alias(libs.plugins.spring.dependency.management)
}

dependencies {
    implementation libs.jspecify
    implementation libs.jserial.comm

    implementation(platform(libs.spring.bom))
    implementation libs.bundles.spring.boot
    implementation("org.springframework:spring-jcl:6.2.15")

    implementation libs.javafx.base
    implementation libs.javafx.graphics

    implementation libs.inject.api

    testImplementation libs.junit.jupiter.api
    testImplementation libs.assertj
    testRuntimeOnly libs.junit.jupiter
    testRuntimeOnly libs.junit.platform.launcher
}

javafx {
    version = '27-ea+1'
    modules = ['javafx.controls', 'javafx.fxml']
    configurations = ['implementation']
}

processResources {
    expand(project.properties)
}

tasks.withType(Test).configureEach {
    jvmArgs('--enable-native-access=ALL-UNNAMED')
}

tasks.withType(JavaExec).configureEach {
    jvmArgs('--enable-native-access=ALL-UNNAMED')
}

tasks.named('run') {
    jvmArgs = ['--enable-native-access=javafx.graphics']
}

application {
    mainModule = 'com.ak.fx.desktop'
    mainClass = 'com.ak.fx.desktop.FxApplication'
    applicationDefaultJvmArgs = ['--enable-native-access=javafx.graphics']
}